#compdef xcpkg

_xcpkg_actions=(
    '-h:show help of this command.'
    '--help:show help of this command.'
    '-V:show version of this command.'
    '--version:show version of this command.'
    'gen-url-transform-sample:generate url-transform sample.'
    'setup:install essential tools.'
    'env:show system and current activated xcode information.'
    'sysinfo:show your system information.'
    'xcinfo:show your current activated xcode information.'
    'integrate:integrate zsh completions.'
    'cleanup:delete the unused cached files.'
    'ls-available:list the available packages.'
    'ls-installed:list the installed packages.'
    'ls-outdated:list the installed packages which can be upgraded.'
    'is-available:is the given package available.'
    'is-installed:is the given package installed.'
    'is-outdated:is the given package outdated.'
    'formula-repo-init:create a new empty formula repository.'
    'formula-repo-add:create a new empty formula repository then sync with server.'
    'formula-repo-del:delete the given formula repository from your local cache.'
    'formula-repo-sync:sync the given formula repository.'
    'formula-repo-conf:change the config of the given formula repository.'
    'formula-repo-info:show information of the given formula repository.'
    'formula-repo-list:list all available formula repositories.'
    'search:search packages can be installed.'
    'info-available:show information of the given available package.'
    'info-installed:show information of the given installed package.'
    'depends:show depends of the given package.'
    'fetch:download formula resources of the given package to the cache.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'upgrade-self:upgrade this software.'
    'tree:list installed files of the given installed package in a tree-like format.'
    'logs:show logs of the given installed package.'
    'pack:pack all the installed files of the given installed package to a single archive file.'
)

function _xcpkg_available_packages() {
    local -a _available_packages
    _available_packages=($(xcpkg ls-available 2>/dev/null))
    _describe 'available-packages' _available_packages
}

function _xcpkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(xcpkg ls-installed 2>/dev/null | sed 's|:|\\:|g'))
    _describe 'installed-packages' _installed_packages
}

function _xcpkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(xcpkg ls-outdated 2>/dev/null | sed 's|:|\\:|g'))
    _describe 'outdated-packages' _outdated_packages
}

function _xcpkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :{_describe xcpkg_actions _xcpkg_actions}' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                ls-target-platform-versions)
                    _arguments ':platform:($(xcpkg ls-target-platform-names))'
                    ;;
                ls-target-platform-archs)
                    _arguments \
                        ':platform:($(xcpkg ls-target-platform-names))' \
                        ':bit:(64bit 32bit all)'
                    ;;
                formula-repo-add)
                    _arguments \
                        '1:formula-repo-name:(name)' \
                        '2:formula-repo-url:(https://)' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to 1]' \
                        '--unpin[set pinned to 0]' \
                        + '(enable)' \
                        '--enable[set enabled to 1]' \
                        '--disable[set enabled to 0]'
                    ;;
                formula-repo-init)
                    _arguments \
                        '1:formula-repo-name:(name)' \
                        '2:formula-repo-url:(https://)' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to 1]' \
                        '--unpin[set pinned to 0]' \
                        + '(enable)' \
                        '--enable[set enabled to 1]' \
                        '--disable[set enabled to 0]'
                    ;;
                formula-repo-conf)
                    _arguments \
                        '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))' \
                        '--url=-[change the formula repo url]' \
                        '--branch=-[change the formula repo branch]' \
                        + '(pin)' \
                        '--pin[set pinned to yes]' \
                        '--unpin[set pinned to no]' \
                        + '(enable)' \
                        '--enable[set enabled to yes]' \
                        '--disable[set enabled to no]'
                    ;;
                formula-repo-del)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                formula-repo-sync)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                formula-repo-info)
                    _arguments '1:formula-repo-name:($(xcpkg formula-repo-list | sed -n "/^name: /p" | cut -c7-))'
                    ;;
                info-available)
                    _arguments \
                        '1:package-name:_xcpkg_available_packages' \
                        '2:key:(--yaml --json pkgtype summary version license web-url git-url git-sha git-ref git-nth src-url src-uri src-sha fix-url fix-uri fix-sha fix-opt res-url res-uri res-sha dep-pkg dep-upp dep-pym dep-plm ccflags xxflags ppflags ldflags bscript binbstd bsystem onstart onready onfinal dopatch prepare install dotweak caveats patches reslist symlink parallel developer)'
                    ;;
                info-installed)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages' \
                        '2:key:(--yaml --json pkgtype summary version license web-url git-url git-sha git-ref git-nth src-url src-uri src-sha fix-url fix-uri fix-sha fix-opt res-url res-uri res-sha dep-pkg dep-upp dep-pym dep-plm ccflags xxflags ppflags ldflags bscript binbstd bsystem onstart onready onfinal dopatch prepare install dotweak caveats patches reslist symlink --prefix --files builtby builtat builtat-rfc-3339 builtat-rfc-3339-utc builtat-iso-8601 builtat-iso-8601-utc builtfor parallel developer)'
                    ;;
                fetch)
                    _arguments \
                        '1:package-name:_xcpkg_available_packages'
                    ;;
                depends)
                    _arguments \
                        '1:package-name:_xcpkg_available_packages' \
                        '-t[specify output format]:output-type:(d2 dot box svg png)' \
                        '-o[specify output filepath or directory]:output-path:_files'
                    ;;
                pack)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages' \
                        '-t[specify output type]:output-type:(zip tar.gz tar.xz tar.lz tar.bz2)' \
                        '-o[specify output filepath or directory]:output-path:_files' \
                        '--keep-session-dir[do not delete the session directory even if successfully packed]'
                    ;;
                tree)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages' \
                        '-a[show all files, including hidden ones]' \
                        '--dirsfirst[list directories before files]' \
                        '-L[level]:level:(3)'
                    ;;
                logs)
                    _arguments \
                        '1:package-name:_xcpkg_installed_packages'
                    ;;
                install)
                    _arguments \
                        ':package-name:_xcpkg_available_packages' \
                        '--target=-[specify which target to be built for]:target:()' \
                        '--jobs=-[specify the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--upgrade[upgrade if it could be]' \
                        '--dry-run[dry-run]' \
                        '--keep-session-dir[do not delete the session directory even if successfully installed]' \
                        '--profile=-[specify build profile]:profile:(debug release)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[very verbose mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]' \
                        '-x[set -x]' \
                        '--dump-go[pass -x argument to go build command]' \
                        '--dump-cc[pass -v argument to clang command]' \
                        '--dump-ld[pass -Wl,-v argument to linker]' \
                        '--dump-env[print all environment variables before starting to build]' \
                        '--dump-net[show detailed http messages]' \
                        '--dump-ninja[pass -v argument to ninja command]' \
                        '--dump-gmake[pass --debug argument to gmake command]' \
                        '--dump-cmake[pass -DCMAKE_FIND_DEBUG_MODE=ON argument to cmake command]' \
                        '--dump-pkg-config[export PKG_CONFIG_DEBUG_SPEW=1]'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_xcpkg_installed_packages' \
                        '--target=-[specify which target to be built for]:target:()' \
                        '--jobs=-[specify the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-session-dir[do not delete the session directory even if successfully installed]' \
                        '--profile=-[specify build profile]:profile:(debug release)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[very verbose mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]' \
                        '-x[set -x]' \
                        '--dump-go[pass -x argument to go build command]' \
                        '--dump-cc[pass -v argument to clang command]' \
                        '--dump-ld[pass -Wl,-v argument to linker]' \
                        '--dump-env[print all environment variables before starting to build]' \
                        '--dump-net[show detailed http messages]' \
                        '--dump-ninja[pass -v argument to ninja command]' \
                        '--dump-gmake[pass --debug argument to gmake command]' \
                        '--dump-cmake[pass -DCMAKE_FIND_DEBUG_MODE=ON argument to cmake command]' \
                        '--dump-pkg-config[export PKG_CONFIG_DEBUG_SPEW=1]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_xcpkg_outdated_packages' \
                        '--target=-[specify which target to be built for]:target:()' \
                        '--jobs=-[specify the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-session-dir[do not delete the session directory even if successfully installed]' \
                        '--profile=-[specify build profile]:profile:(debug release)' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        + '(verbose_level)' \
                        '-vv[very verbose mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]' \
                        '-x[set -x]' \
                        '--dump-go[pass -x argument to go build command]' \
                        '--dump-cc[pass -v argument to clang command]' \
                        '--dump-ld[pass -Wl,-v argument to linker]' \
                        '--dump-env[print all environment variables before starting to build]' \
                        '--dump-net[show detailed http messages]' \
                        '--dump-ninja[pass -v argument to ninja command]' \
                        '--dump-gmake[pass --debug argument to gmake command]' \
                        '--dump-cmake[pass -DCMAKE_FIND_DEBUG_MODE=ON argument to cmake command]' \
                        '--dump-pkg-config[export PKG_CONFIG_DEBUG_SPEW=1]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_xcpkg_installed_packages'
                    ;;

                is-available)
                    _arguments '1:package-name:_xcpkg_available_packages'
                    ;;
                is-installed)
                    _arguments '1:package-name:_xcpkg_installed_packages'
                    ;;
                is-outdated)
                    _arguments '1:package-name:_xcpkg_outdated_packages'
                    ;;

                ls-available)
                    _arguments '1:package-name:_xcpkg_available_packages' '-v[verbose mode]'
                    ;;
                ls-installed)
                    _arguments '1:package-name:_xcpkg_installed_packages' '-v[verbose mode]'
                    ;;
                ls-outdated)
                    _arguments '1:package-name:_xcpkg_outdated_packages' '-v[verbose mode]'
                    ;;

                integrate)
                    _arguments '1:reg:()' '-v[verbose mode]'
                    ;;

                integrate)
                    _arguments '1:what:(zsh)'
                    ;;
            esac
            ;;
        *);;
    esac
}

_xcpkg "$@"
